	
	* Purpose: Tagging observations based on selected variables that contain the same value
	* Author: Fahad Mirza

	
	* Loading example data
	* Example generated by -dataex-. For more info, type help dataex in Stata
	clear
	input str3 name float(var1 var2 var3 var4 var5)
	"Yes" 888 888 888 888 888
	"Yes"   2   1   2   1   2
	"Yes"   2   2   2   2   2
	"Yes"   4   4   4   4   4
	"Yes"   0   1   1   1   1
	"Yes"   0   1   1   1   1
	"Yes"   1   .   1   .   1
	"Yes"   0   1   1   1   1
	"Yes"   0   0   1   0   0
	"Yes"   0   .   1   .   1
	"Yes"   0   0   0   0   0
	end

	
	* Variables on which we require a tag
	* Note: Only add required variables here. The code does the rest
	* Note: Run in One go
	ds var1 var3 var5
	
	****************************************************************************
	
	* Saving the selected variables in a local
	local variables `r(varlist)'
	
	* Creating a word count of the number of variables we want to run code
	local n : word count `variables'
	
	* Looping over conditions and generating a complete text condition to be used later
	forvalues i = 1/`n' {
		
		local a : word `i' of `variables'
		local b : word `=`i' + 1' of `variables'
		
		if `i' < `n' {
			local condition "`condition' (`a' == `b') &"
		}
		
		else {
			local a : word 1 of `variables'
			local b : word `n' of `variables'
			local condition "`condition' (`a' == `b')"
		}
		
	}
	
	* Displaying the condition that we generated
	display "`condition'"
	
	* Tagging duplicates by row on the variables we require
	generate tag = 1 if `condition'
	
	
	
